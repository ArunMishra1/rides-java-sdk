apply plugin: 'distribution'
apply plugin: 'net.researchgate.release'
apply plugin: 'co.riiid.gradle'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.7'
        classpath 'net.researchgate:gradle-release:2.2.2'
        classpath 'co.riiid:gradle-github-plugin:0.3.1'
    }
}

def unsnapshottedVersion = version.replaceAll("-SNAPSHOT", "");

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'maven'
    apply plugin: 'net.saliman.cobertura'

    repositories {
        jcenter()
    }

    dependencies {
        compile 'org.slf4j:slf4j-log4j12:1.7.5'
    }

    checkstyle {
        toolVersion = "6.4.1"
    }

    cobertura {
        coverageFormats = ['html', 'xml']
        coverageIgnoreTrivial = true
        coverageCheckTotalLineRate = 100
        coverageCheckTotalBranchRate = 100
    }

    task checkstyleMain(type: Checkstyle, overwrite: true) {
        configFile = new File("{$project.projectDir}/config/checkstyle/checkstyle-main.xml")
    }

    task checkstyleTest(type: Checkstyle, overwrite: true) {
        configFile = new File("{$project.projectDir}/config/checkstyle/checkstyle-test.xml")
    }

    task githubReleaseZip(type: Zip) {
        version = "v" + unsnapshottedVersion
        from('.') {
            filter { String line ->
                line.replaceAll("compile project\\(':sdk'\\)", "compile '${group}:${artifactId}:${unsnapshottedVersion}'")
            }
            into '.'
            exclude 'build'
            exclude '*.iml'
        }
    }

    release {
        failOnCommitNeeded = false
        failOnPublishNeeded = false
        failOnSnapshotDependencies = false
        revertOnFail = true
        tagTemplate = "v${unsnapshottedVersion}"
    }
}

afterReleaseBuild.dependsOn ":sdk:uploadArchives"
updateVersion.dependsOn ":githubRelease"
githubRelease.dependsOn ":samples:cmdline-sample:githubReleaseZip"//, ":servletsampleDistZip"
//System.out.println(task(":samples:cmdline-sample:zip").archivePath)

github {
    owner = 'uber'
    repo = 'rides-java-sdk'
    token = githubToken
    tagName = "v${unsnapshottedVersion}"
    targetCommitish = 'master'
    name = "v${unsnapshottedVersion}"
    body = "sample"

    assets = [
            project(":samples:cmdline-sample").githubReleaseZip.archivePath,
            //"${project.projectDir}/build/distributions/servlet-sample-${version}.zip",
    ]
}

distributions {
    publicrepo {
        baseName = 'publicrepo'
        contents {
            from(rootDir) {
                include 'build.gradle'
                include 'CHANGELOG.md'
                include 'gradle.properties'
                include 'gradlew'
                include 'gradlew.bat'
                include 'LICENSE'
                include 'settings.gradle'
                include 'gradle/'
            }

            from(rootDir) {
                include 'README.md'
                filter { String line ->
                    line.replaceAll("@version@", unsnapshottedVersion)
                }
            }

            from('sdk') {
                filter { String line ->
                    line.replaceAll("@version@", unsnapshottedVersion)
                }
                exclude 'build'
                exclude '*.iml'
                into 'sdk'
            }

            from('samples') {
                exclude '**/build'
                exclude '**/*.iml'
                into 'samples'
            }
        }
    }
}
